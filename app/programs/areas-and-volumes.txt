# Areas & Volumes (Sign Posts)

# What is the area of a square and the volume of a cube with a side of 6.17?
# What is the area of a circle and the volume of a sphere with radius of 2.98?
# Answer: 38.07, 234.89, 27.9 and 110.85.
# Run the Program then check the details in the History.

# Source: "TI-57 Making Tracks into Programming.pdf#page=66", p. 3-23

# Data Input
6.17 STO 0     # Side
2.98 STO 1     # Radius

# Data Processing (38 steps)
2nd Fix 2
RCL 0
SBR 1          # Calculate the area of a square
2nd Pause
RCL 0
SBR 3          # Calculate the volume of a cube
2nd Pause
RCL 1
SBR 2          # Calculate the area of a circle
2nd Pause
RCL 1
SBR 4          # Calculate the volume of a sphere
R/S

# Subroutine: Area of a square
2nd lbl 1
x^2            # Side²
INV SBR

# Subroutine: Area of a circle
2nd Lbl 2
x^2 X 2nd pi = # Radius² X π
INV SBR

# Subroutine: Volume of a cube (5 steps)
2nd Lbl 3
y^x 3 =        # Side³
INV SBR

# Subroutine: Volume of a sphere
2nd Lbl 4
y^x 3 X 4 : 3 X 2nd pi = # Radius^3 X 4:3π
INV SBR
