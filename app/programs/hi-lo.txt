# Hi-Lo

# How many guesses will you take to discover a secret number between 0 and 1023?
# Enter a number in the Display Register then press Return.
# The calculator displays +1 if your guess is too large, -1 if too small or 0 if correct!
# Check the History to narrow down the number.

# Source: "Training with your EC-4000 Programmable Calculator.pdf#page=214", p. 11-11

# Data Input
0     STO 2          # Your guess

# Data Preprocessing
0 STO 7
RCL 0
INV 2nd x=t         # Seed != 0?
GTO 0
0.123456 STO 0      # Set the seed
2nd Lbl 0

# Data Processing (50 steps)
0 STO 7
RCL 1
2nd x=t              # No tries yet?
SBR 1                # Generate secret
SBR 4                # Test the guess
R/S

# Subroutine: Generate the secret number
2nd Lbl 1
2nd pi + RCL 0 =     # i = Ï€ + A
y^x 8 = INV 2nd Int  # A = Fract[i^8]
STO 0
X 1023 + 1 = 2nd Int # R = Int(A X 1023 + 1)
STO 6                # Secret
INV SBR

# Subroutine: Test the guess
2nd Lbl 4
RCL 6 STO 7
1 SUM 1              # Increment the tries
RCL 2
2nd Pause
INV 2nd x>=t         # Guess < Secret?
GTO 5                # Yes, too low
2nd x=t              # Guess = Secret?
GTO 7                # Yes, correct
1 =                  # +1, too high
INV SBR
2nd Lbl 5
1 +/- =              # -1, too low
INV SBR              #
2nd Lbl 7            # Good guess
0 STO 1              # Reset the tries
INV SBR
