# Evaluating Integrals: Simpson's Rule

# What is the area between 1 and 5 under y = 2x² + 3x - 1?
# Answer: 114.666...
# Tip! The function can easily be changed in the subroutine.

# Formula: Area = w/3 (Y1 + 4Y2 + 2Y3 + 4Y4 + ... + 2Yn-1 + 4Yn + Yn+1)
# w = (b - a) / n

# Source: "TI-57 Making Tracks into Programming.pdf#page=174", p. 9-6

# Data Input
6 STO 0             # n
5 STO 1             # b
1 STO 2             # a

# Data Processing (49 steps)
RCL 2 INV SUM 1     # b -= a
RCL 0 INV 2nd Prd 1 # b /= n
RCL 1 STO 5         # w = (b - a) / n
0 STO 3             # Reset the area
1                   # Y1
2nd Lbl 0
STO 4               # c = 1, 2 or 4
SBR 1 X RCL 4 =     # cY
SUM 3               # Area += cY
RCL 1 SUM 2         # a += w
2nd Dsz             # --n != 0?
GTO 2               # Yes
SBR 1 SUM 3         # No, Area += Yn+1
RCL 3 X RCL 5 : 3 = # Area /= w/3
R/S
2nd Lbl 2
RCL 4 x<>t
4 2nd x=t           # c = 4?
2                   # Yes, c to be equal to 2
GTO 0               # No, c to be equal to 4

# Subroutine: The function, ex. y = 2x² + 3x - 1
2nd lbl 1
RCL 2 x^2 X 2 + 3 X RCL 2 - 1 =
INV SBR
