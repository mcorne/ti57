# Lunar Lander

# You are in a Lunar Lander at an altitude of 5 000 m with 130 kg left of propellant.
# Your job is to touch the surface at less than 5 m/s by adjusting the consumption every 10 sec.
# So, go ahead! Keep or change the consumption rate then hit return!
# The calculator will display the current speed and height in one number, ex. -96.4020.
# That means you are descending at 96 m/s at an altitude of 4 020 m.
# Click on the history to check how much propellant you have left.
# At some point, 1111.1111 will show if you landed safely or 9999.9999 if you crashed!
# You can also see 5555.5555 if you are trying to consume more than you have left...
# Good luck!

# Source: "Ordinateur de poche n°8.pdf#page=52", p. 54

# Input Data
10      STO 4      # Consumption rate (n)
RCL 5
INV 2nd x=t        # Tries != 0?
GTO 0              # Yep, descending
                   # Nope (new game)
130     STO 0      # Propellant (P)
5000    STO 1      # Altitude (H)
100 +/- STO 2      # Speed (V)

# Entry Point
2nd Lbl 0
RCL 4 STO 7
RCL 0 INV 2nd x>=t # P < n?
GTO 2              # Not enough propellant
1 SUM 5            # Add one try
RCL 4
INV SUM 0          # P -= n
RCL 0
INV 2nd Fix 2nd Pause 2nd Fix 4
10 X RCL 2         # 10V
+ (2 X RCL 4 - 16) # ΔV = 2n - 16
SUM 2              # V += Δv
X 5 =              # ΔH = 10V + 5ΔV
SUM 1              # H += ΔH
# V.H = H/1000 + Int(|V|), ex. 96.4020
RCL 1 : 10000 + RCL 2 2nd |x| 2nd Int =
STO 6
RCL 2 STO 7
0 INV 2nd x>=t     # V >= 0?
GTO 4              # Going up!
RCL 6 +/- STO 6    # V.H = -V.H
2nd Lbl 4
RCL 6
2nd Pause
2nd Fix 4
RCL 1 STO 7
0 INV 2nd x>=t     # H < 0?
GTO 3              # Not landed yet
0 STO 5
5 STO 7
RCL 2 2nd |x|
2nd x>=t           # |V| >= 5?
GTO 1              # Too fast!
1111.1111          # Landed!
R/S
2nd Lbl 1
9999.9999          # Crashed!
R/S
2nd Lbl 2
5555.5555          # Not enough propellant
R/S
2nd Lbl 3
RCL 6              # Still descending...
R/S
