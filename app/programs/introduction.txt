# Introduction to programming the TI-57 (Dice Roll)

# The TI-57 is a programmable pocket calculator made by Texas Instruments around 1980.
# It has a LED display, 50 program steps and 8 memory registers.
# A TI-57 program is a set of basic instructions that can perform calculations,
# read or write memory, test a value, go to a program step to make a loop or call a subroutine.

# The TI-57 program emulator (this web site) translates the instructions into Python then runs it.
# The Python code can be checked below along with the calculator internal state.
# As an example, this program generates a random number (R) from 1 to 6 to simulate a dice roll.
# It implements the following algorithm:
# A = Fract[(π + A)^8] where A = 0 for the very first run
# R = Int(A X 6 + 1)

# Source: [https://en.wikipedia.org/wiki/TI-57 TI-57] on Wikipedia

# Input Data
1 STO 0             # Number of dice

# Entry Point
0 STO 1             # Reset the total
2nd Lbl 1
2nd pi + RCL 2 =    # i = π + A
y^x 8 = INV 2nd Int # A = Fract[i^8]
STO 2
X 6 + 1 = 2nd Int   # R = Int(A X 6 + 1)
SUM 1               # Update the total
2nd DSZ             # Dice left to roll?
GTO 1               # Yes, roll the next dice
RCL 1               # No, display the total
R/S
