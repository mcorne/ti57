# Exponential Growth and Decay

# You are watching a culture of bacteria.
# It starts with a count of 500, and growth to 1500 after 5 hours.
# What is the k factor ?
# How many bacteria will there be after 10 hours? 11? 12? ... 24?
# After how many hours will the bacteria count reach 1 million?
# Answer: 4500.0, 5605.8, 6983.3, ..., 97533.1, 34.6.
# Run the Program then check the details in the History.

# Source: "TI-57 Making Tracks into Programming.pdf#page=197", p. 10-10

# Data Input
500     STO 7 # Initial count (n0)
1500    STO 1 # Count after n hours (n)
5       STO 2 # Number of hours (t)
10      STO 4 # Growth time
1       STO 5 # Time increment
14      STO 0 # Duration
1000000 STO 6 # Target count

# Data Preprocessing
2nd Fix 1
SBR 0
SBR 1
SBR 2
CLR
SBR 3

# Data Processing
R/S

# Subroutine: Calculate k
2nd lbl 0
RCL 1 : RCL 7 =
Lnx : RCL 2 =
STO 3
INV SBR

# Subroutine: Calculate the growth
2nd lbl 1
RCL 7 X (RCL 3 X RCL 4) INV Lnx =
2nd Pause
INV SBR

# Subroutine: Calculate the growth over time
2nd lbl 2
RCL 5 SUM 4
RCL 4
2nd Pause
SBR 1
2nd Dsz
GTO 2
INV SBR

# Subroutine: Calculate the time to grow
2nd lbl 3
RCL 6 : RCL 7 =
Lnx : RCL 3 =
INV SBR
