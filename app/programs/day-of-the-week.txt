# Day of the Week

# Enter the day, month, century and year then run to get the day of the week:
# Sunday=0, Monday=1, Tuesday=2, Wednesday=3, Thursday=4, Friday=5 or Saturday=6.
# So, what day of the week was July 4, 1776?
# Answer: a Thursday (the 4th day of the week).

# Tip! W = (k + Int(2.6m − 0.2) −2C + Y + Int(Y/4) + Int(C/4)) mod 7
# k = Day (1 to 31)
# m = Month (March=1, etc.)
# C = Century, ex. 19 for 1987
# Y = Year, ex. 87 for 1987, remove 1 for Jan and Feb
# W = Day of the week

# Source: "Frequently Asked Questions in Mathematics.pdf#page=64", p. 63

# Input Data
4  STO 1    # Day (1-31)
7  STO 2    # Month (1-12)
17 STO 3    # Century
76 STO 4    # Year
SBR 1

# Entry Point
RCL 1
+ (2.6 X RCL 2 - 0.2) 2nd Int
- 2 X RCL 3
+ RCL 4
+ (RCL 4 : 4) 2nd Int
+ (RCL 3 : 4) 2nd Int =
: 7 = INV 2nd Int X 7 = 2nd Fix 0 2nd |x|
R/S

# Subroutine 1: convert the month to Mar=1, Apr=2 etc.
2nd Lbl 1
2 INV SUM 2 # Month - 2
1 x<>t
RCL 2
2nd x>=t    # Month >= 1?
INV SBR     # Mar=1 to Dec=10
12 SUM 2    # Jan=11, Feb=12
1 INV SUM 4 # Year - 1
1 +/- x<>t
RCL 4
INV 2nd x=t # Year != -1?
INV SBR     # Year OK
99 STO 4    # Year = 99
1 INV SUM 3 # Century - 1
INV SBR
