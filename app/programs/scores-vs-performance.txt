# Scores vs Performance (Linear Regression)

# You want to evaluate a test for prospective employees.
# You have both the employee test score and the actual sales performance.
# What are the slope, the y intercept and the correlation?
# What sales performance would a test score of 9 predict?
# What test score would indicate a sales performance 75% above target?
# Answer: 2.68, 6.14, 0.75, 0.87, 30.29, 25.66.
# Run the Program then check the details in the History.

# Source: "TI-57 Making Tracks into Programming.pdf#page=182", p. 9-14

# Data Input
5  x<>t 10 2nd S+
13 x<>t 30 2nd S+
8  x<>t 30 2nd S+
10 x<>t 40 2nd S+
15 x<>t 60 2nd S+
20 x<>t 50 2nd S+
4  x<>t 20 2nd S+
16 x<>t 60 2nd S+
18 x<>t 50 2nd S+
6  x<>t 20 2nd S+

# Data Preprocessing
2nd Fix 2
SBR 0    2nd Pause
SBR 1    2nd Pause
SBR 2    2nd Pause
Vx       2nd Pause
9  SBR 3 2nd Pause
75 SBR 4 2nd Pause

# Data Processing (49 steps)
R/S

# Subroutine: Calculate the slope (m)
2nd lbl 0
RCL 5 : RCL 0 - 2nd x X INV 2nd x =
: INV 2nd s2 =
INV SBR

# Subroutine: Calculate the y intercept (b)
2nd lbl 1
SBR 0 X INV 2nd x +/- + 2nd x =
INV SBR

# Subroutine: Calculate the correlation (rÂ²)
2nd lbl 2
SBR 0 x^2 : 2nd s2 X INV 2nd s2 =
INV SBR

# Subroutine: Calculate y given x
2nd lbl 3
STO 7 SBR 0 2nd Prd 7 SBR 1 SUM 7 RCL 7
INV SBR

# Subroutine: Calculate x given y
2nd lbl 4
STO 7 SBR 1 INV SUM 7 SBR 0 INV 2nd Prd 7 RCL 7
INV SBR
